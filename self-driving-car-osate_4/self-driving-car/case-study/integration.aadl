package integration
public
	
with devices;

with data_acquisition;
--with image_processing;
--with distance_processing;
--with overtaking_detection_processing;
--with speed_processing;
with panel_processing;
with brake_acceleration_processing;
with hardware;

system integration
end integration;
	
system implementation integration.functional
subcomponents	
-- input devices
	camera					: device devices::camera;
	radar1					: device devices::radar1;
	radar2					: device devices::radar2;
	traffic_lane_sensor		: device devices::traffic_lane_sensor;
	induction_speed_sensor	: device devices::induction_speed_sensor;
	optical_speed_sensor	: device devices::optical_speed_sensor;
	acceleration_pedal		: device devices::acceleration_pedal;
	
-- software and processing

	-- changed to threads in data_acquisition process
	--	image_analysis			: process image_processing::image_analysis.i;
	--	distance_voter			: process distance_processing::distance_voter.i;
	--	overtaking_detection	: process overtaking_detection_processing::overtaking_detection.i;
	--	speed_voter				: process speed_processing::speed_voter.i;
	data_acquisition		: process data_acquisition::data_acquisition.i;
	panel_controller		: process panel_processing::panel_controller.i;
	speed_controller		: process brake_acceleration_processing::speed_controller.i;
	
-- output devices
	brake 					: device devices::brake;
	accelerator 			: device devices::accelerator;
	panel					: device devices::panel;
connections
-- inputs to processes	
	c00 : port camera.picture 						  	-> data_acquisition.picture;
	c01 : port radar1.distance							-> data_acquisition.distance1;
	c02 : port radar2.distance							-> data_acquisition.distance2;
	c03 : port traffic_lane_sensor.lane_crossed 		-> data_acquisition.lane_change;
	c04 : port induction_speed_sensor.speed				-> data_acquisition.speed1;
	c05 : port optical_speed_sensor.speed				-> data_acquisition.speed2;
	c06 : port acceleration_pedal.speed 				-> panel_controller.desired_speed;
	c07 : port acceleration_pedal.speed 				-> speed_controller.desired_speed;
-- processes to processes
	c08 : port data_acquisition.obstacle_detected 		-> speed_controller.obstacle_detected;
	c09 : port data_acquisition.speed_result 			-> speed_controller.current_speed;
	c10 : port data_acquisition.overtaking_active 		-> speed_controller.overtaking_active;
	c11 : port data_acquisition.distance_result 		-> speed_controller.current_distance;
	c12 : port data_acquisition.speed_result			-> panel_controller.current_speed;
-- processes to output 
	c13 : port speed_controller.brake_cmd				-> brake.brake_cmd;
	c14 : port speed_controller.accelerate_cmd			-> accelerator.accelerate_cmd;
	c15 : port panel_controller.current_speed_result	-> panel.current_speed;
	c16 : port panel_controller.desired_speed_result	-> panel.desired_speed;
flows
	radar1_to_brake : end to end flow radar1.f0 -> c01 -> data_acquisition.f1 -> c11 -> speed_controller.f02 -> c13 -> brake.f0;
			
end integration.functional;



system implementation integration.hardware extends integration.functional
subcomponents
	cpu1 : processor hardware::ecu;
	cpu2 : processor hardware::ecu;
	can1 : bus hardware::can;
	can2 : bus hardware::can;
	can3 : bus hardware::can;
connections
	b00 : bus access can1 <-> cpu1.socket1;
	b01 : bus access can2 <-> cpu2.socket1;
	b02 : bus access can3 <-> cpu1.socket2;
	b03 : bus access can3 <-> cpu2.socket2; 
	properties
	
	-- ref. CPU1
	actual_processor_binding => (reference (cpu1)) applies to data_acquisition, speed_controller;
	-- ref. CPU2
	actual_processor_binding => (reference (cpu2)) applies to panel_controller;
	-- ref. can1
	actual_connection_binding => (reference (can1)) applies to c00,c01,c02,c03,c04,c05,c07,c13,c14;
	-- ref. can2
	actual_connection_binding => (reference (can2)) applies to c06,c15,c16;
    -- ref. can3
	actual_connection_binding => (reference (can3)) applies to c12;
end integration.hardware;

	
end integration;